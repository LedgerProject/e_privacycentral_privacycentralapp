plugins {
	id 'com.android.application'
	id 'kotlin-android'
}

android {
	compileSdkVersion buildConfig.compileSdk

	defaultConfig {
		applicationId "foundation.e.privacycentralapp"
		minSdkVersion buildConfig.minSdk
		targetSdkVersion buildConfig.targetSdk
		versionCode buildConfig.version.code
		versionName buildConfig.version.fullName

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

		resValue("string", "mapbox_key", MAPBOX_KEY)
	}

	// We define here  the OS flavor e, specific for the /e/ OS version, and google, for any
	// Andriod device. The e or google prefix is then used in resources, dependencies, ... as
	// expected by the android gradle plugin.
	flavorDimensions 'os'
	productFlavors {
		e {
			applicationIdSuffix '.e'
			dimension 'os'
		}
		google {
			applicationIdSuffix '.google'
			dimension 'os'
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	signingConfigs {
		debug {
			storeFile rootProject.file(".sign/debug.keystore")
			storePassword "android"
			keyAlias "androiddebugkey"
			keyPassword "android"
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}


}

dependencies {
	compileOnly files('libs/e-ui-sdk-1.0.1-q.jar')
	implementation project(":privacymodulesapi")

	// include the google specific version of the modules, just for the google flavor
	googleImplementation project(":privacymodulesgoogle")
	// include the e specific version of the modules, just for the e flavor
	eImplementation project(":privacymodulese")
	implementation project(":flow-mvi")
	implementation Libs.Kotlin.stdlib
	implementation Libs.AndroidX.coreKtx
	implementation Libs.AndroidX.Fragment.fragmentKtx
	implementation 'androidx.appcompat:appcompat:1.2.0'
	implementation Libs.AndroidX.Lifecycle.runtime
	implementation Libs.AndroidX.Lifecycle.viewmodel

	implementation Libs.MapBox.sdk
	implementation 'com.google.android.material:material:1.3.0'

	testImplementation 'junit:junit:4.+'
	androidTestImplementation 'androidx.test.ext:junit:1.1.2'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
